/*
 * Copyright (C) 2019.  Arnaud 'Bluexin' Sol√©
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'net.minecraftforge.gradle.forge'

version = modVersion
group = modGroup
archivesBaseName = "$modBaseName-$mcVersion"

minecraft {
    version = project.forgeVersion
    runDir = 'run'
    mappings = project.mcpVersion
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = archivesBaseName

            afterEvaluate {
                artifact(reobfBundleJar.jar) {
                    builtBy reobfBundleJar
                }
            }
        }
    }

    repositories {
        maven {
            url = "file://$buildDir/repo"
        }
    }
}

reobf {
    jar {
        extraLines += ["PK: gnu/jel saoui/shade/gnu/jel"]
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url = 'https://maven.bluexin.be/repository/snapshots/'
    }
    maven {
        url = 'https://maven.bluexin.be/repository/releases/'
    }
//    maven { url "https://kotlin.bintray.com/kotlinx" }
}

configurations {
    contained {
        transitive = false
    }
}

dependencies {
    compile project(':coremod')
    contained project(':coremod')
    compile('com.saomc:saoui:1.12.2-2.0.0.13-SNAPSHOT:deobf') {
        exclude group: 'com.teamwizardry.librarianlib'
    }
//    compile 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1'
//    contained 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.1'
//    contained 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.9.1'

    contained("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version")
    contained("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")

    compile 'com.teamwizardry.librarianlib:librarianlib-1.12.2:4.17-SNAPSHOT'
    compile 'moe.plushie:armourers_workshop-1.12.2:0.49.0-SNAPSHOT:deobf'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module { inheritOutputDirs = true } }

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 10, 'minutes'
}

jar {
    manifest {
        attributes([
                'FMLAT'         : 'bluerpg_at.cfg',
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'     : System.currentTimeMillis()
        ])
    }
}

task bundleJar(type: Jar) {
    classifier 'bundle'

    from sourceSets.api.output
    from sourceSets.main.output

    from(configurations.contained.resolvedConfiguration.resolvedArtifacts.collect {
        def r = fileTree(it.file)
        if (!it.file.exists()) r
        else {
            def ft = zipTree(it.file)
            def f = (File) ft.find { it.name == "MANIFEST.MF" }
            if (f == null || !f.text.contains("Maven-Artifact")) {
                def nf = file("$buildDir/tmp/${it.file.name}.meta")
                if (!nf.exists()) nf.text = "Maven-Artifact: $it.moduleVersion"
                r += nf
            }
            r
        }
    }) {
        into 'META-INF/libraries'
    }

    manifest {
        attributes([
                'FMLAT'         : 'bluerpg_at.cfg',
                'ContainedDeps' : configurations.contained.files.collect { it.name }.join(' '),
                'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
                'Timestamp'     : System.currentTimeMillis()
        ])
    }
}

reobf.create('bundleJar') {
    extraLines += ["PK: gnu/jel saoui/shade/gnu/jel"]
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    manifest {
        attributes 'FMLAT': 'bluerpg_at.cfg'
    }
    classifier = 'deobf'
}

compileKotlin {
    kotlinOptions {
        javaParameters = true
        jvmTarget = 1.8
        freeCompilerArgs += "-Xjvm-default=enable"
    }
}
